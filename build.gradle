plugins {
    id 'com.adarshr.test-logger' version "4.0.0" apply false
    id 'org.springframework.boot' version "$springBootVersion" apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.adarshr.test-logger'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        // for local development
        mavenLocal()
        // take everything from central
        mavenCentral()
        // also support snapshot / milestone releases of spring
        maven { url 'https://repo.spring.io/milestone/' }
    }

    dependencies {
        testImplementation(platform('org.junit:junit-bom:6.0.0'))
        testImplementation('org.junit.jupiter:junit-jupiter')
        testRuntimeOnly('org.junit.platform:junit-platform-launcher')
    }

    test {
        useJUnitPlatform()
    }

    configurations.configureEach {
        exclude group: 'junit', module: 'junit'
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.compilerArgs << '-parameters'
        options.compilerArgs << '-Xlint:deprecation'
    }
}
